configfile: "config/config.yaml"


if config.get("load_fair_genome_indexer", True):

    include: "rules/fair_genome_indexer.smk"


if config.get("load_fair_fastqc_multiqc", True):

    include: "rules/fair_fastqc_multiqc.smk"


include: "rules/common.smk"
include: "rules/bowtie2_sambamba_meta.smk"
include: "rules/deeptools.smk"
include: "rules/fastp.smk"
include: "rules/goleft.smk"
include: "rules/multiqc.smk"
include: "rules/ngsderive.smk"
include: "rules/picard_metrics.smk"
include: "rules/rseqc.smk"
include: "rules/samtools_stats.smk"


rule fair_bowtie2_mapping_target:
    input:
        unpack(get_fair_bowtie2_mapping_target),
    output:
        genomes="reference/genomes/fair_bowtie2_mapping.csv",
    threads: 1
    resources:
        mem_mb=lambda wildcards, attempt: 512 * attempt,
        runtime=lambda wildcards, attempt: 10 * attempt,
        tmpdir="tmp",
    log:
        "logs/fair_bowtie2_mapping_target.log",
    benchmark:
        "benchmark/fair_bowtie2_mapping_target.tsv"
    params:
        genomes=genomes.copy(),
    conda:
        "envs/python.yaml"
    script:
        "scripts/regenerate_genome.py"
