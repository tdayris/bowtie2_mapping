include: "rules/common.smk"
include: "rules/fastp.smk"


if config.get("load_fair_genome_indexer", True):

    include: "rules/fair_genome_indexer_pipeline.smk"


if config.get("load_fair_fastqc_multiqc", True):

    include: "rules/fair_fastqc_multiqc_pipeline.smk"


include: "rules/bowtie2_sambamba_meta.smk"
include: "rules/picard_metrics.smk"
include: "rules/samtools_stats.smk"
include: "rules/multiqc.smk"


rule fair_bowtie2_mapping_target:
    input:
        unpack(get_fair_bowtie2_mapping_target),
    output:
        genomes="reference/genomes/fair_bowtie2_mapping.csv",
    threads: 1
    resources:
        mem_mb=lambda wildcards, attempt: 512 * attempt,
        runtime=lambda wildcards, attempt: 10 * attempt,
        tmpdir="tmp",
    log:
        "logs/fair_bowtie2_mapping_target.log",
    benchmark:
        "benchmark/fair_bowtie2_mapping_target.tsv"
    conda:
        "envs/python.yaml"
    script:
        "scripts/regenerate_genome.py"
